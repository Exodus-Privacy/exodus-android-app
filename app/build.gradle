plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'org.jlleitschuh.gradle.ktlint'
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32
        versionCode 12
        versionName "2.2.1"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        buildConfigField "String", "EXODUS_API_KEY", "\"$System.env.EXODUS_API_KEY\""
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }
    buildFeatures {
        viewBinding true
    }
    lint {
        lintConfig file("lint.xml")
    }
    namespace 'org.eu.exodus_privacy.exodusprivacy'
}

kapt {
    correctErrorTypes true
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.core:core-splashscreen:1.0.0-rc01'
    implementation 'io.noties.markwon:core:4.6.2'
    implementation 'androidx.window:window:1.0.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.4.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.browser:browser:1.4.0'
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.2'
    testImplementation 'junit:junit:4.13.2'

    // Navigation Components
    def nav_version = "2.4.2"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Retrofit and Moshi
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    // Hilt
    def hilt_version = '2.42'
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "com.google.dagger:hilt-android:$hilt_version"

    // Lifecycle
    def lifecycle_version = "2.4.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"

    // Room
    def room_version = "2.4.2"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"

    // KTX
    implementation "androidx.fragment:fragment-ktx:1.4.1"
    implementation "androidx.activity:activity-ktx:1.4.0"

}
